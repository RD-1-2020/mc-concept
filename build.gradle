import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias libs.plugins.spring.boot apply false
    alias libs.plugins.spring.dependency.management
}

allprojects {
    group = 'org.azurecloud.solutions'
    version = '1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.java.get())
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${libs.versions.springCloud.get()}"
        }
    }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation libs.spring.boot.starter.test
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
}
